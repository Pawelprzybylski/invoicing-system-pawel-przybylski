buildscript {
    ext {
        jacksonVersion = '2.11.1'
        lombokVersion = '1.18.16'
        mongoVersion = '3.12.12'
        spockVersion = '1.3-groovy-2.5'
        springBootVersion = '2.4.4'
        springFoxStarterVersion = '3.0.0'
        springFoxUiVersion = '2.9.2'
        flywayVersion = '7.11.3'
    }
}
plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'groovy'
    id 'checkstyle'
    id 'jacoco'
}
group = 'pl.futurecollars.invoicing'
version = '0.0.1-SNAPSHOT'
repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "io.springfox:springfox-boot-starter:${springFoxStarterVersion}"
    implementation "io.springfox:springfox-swagger-ui:${springFoxUiVersion}"
    implementation "org.mongodb:mongo-java-driver:${mongoVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql:42.2.19'
    runtimeOnly 'com.h2database:h2:1.4.200'
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
        vendor = JvmVendorSpec.ADOPTOPENJDK
    }
}
test {
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    executionData tasks.withType(Test)
    dependsOn check
}
jacoco {
    toolVersion = "0.8.6"
}
jacocoTestCoverageVerification {
    executionData tasks.withType(Test)
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.80
            }
        }
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.85
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "pl/futurecollars/invoicing/App.class"
            ])
        }))
    }
}
checkstyle {
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}
task testWithFileDatabase(type: Test) {
    systemProperty 'spring.profiles.active', 'file'
}
task testWithMemoryDatabase(type: Test) {
    systemProperty 'spring.profiles.active', 'memory'
}
task testWithSQLDatabase(type: Test) {
    systemProperty 'spring.profiles.active', 'sql'
}
task testWithJpaDatabase(type: Test) {
    systemProperty 'spring.profiles.active', 'jpa'
}

task testWithJpaFlywayDatabase(type: Test) {
    systemProperty 'spring.profiles.active', 'jpa-flyway'
}



    task testWithMongoDatabase(type: Test) {
        systemProperty 'spring.profiles.active', 'mongo'
    }

    check.dependsOn testWithFileDatabase
    check.dependsOn testWithMemoryDatabase
    check.dependsOn testWithSQLDatabase
    check.dependsOn testWithJpaDatabase
    check.dependsOn testWithJpaFlywayDatabase
    check.dependsOn testWithMongoDatabase

    String osName = System.getProperty("os.name").toLowerCase()
    if (osName.contains("mac")) {
        task testWithPostgreSqlDatabase(type: Test) {
            systemProperty 'spring.profiles.active', 'sql-pg'
        }

    }

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Werror"
}